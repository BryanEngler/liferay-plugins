buildscript {
	repositories {
		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}

	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.node", version: "2.0.0"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "1.0.12"
	}
}

configure(subprojects.findAll {!it.childProjects}) {
	apply plugin: "com.liferay.poshi.runner"

	configurations {
		poshiRunner {
			resolutionStrategy {
				force group: "com.google.guava", name: "guava", version: "16.0.1"
				force group: "commons-codec", name: "commons-codec", version: "1.10"
			}
		}
	}

	poshiRunner {
		baseDir = "test/functional"

		String[] testPropertiesFileNames = [
			"test.properties",
			"test.${System.env.HOSTNAME}.properties",
			"test.${System.env.HOST}.properties",
			"test.${System.env.COMPUTERNAME}.properties",
			"test.${System.env.USER}.properties"
		]

		for (String testPropertiesFileName in testPropertiesFileNames) {
			File testPropertiesFile = file("test/functional/${testPropertiesFileName}")

			if (testPropertiesFile.exists()) {
				poshiProperties << GUtil.loadProperties(testPropertiesFile)
			}
		}

		poshiPropertiesFile = rootProject.file("${baseDir}/test.properties")
		openCVVersion = "2.4.10-0.10"
		version = "1.0.78"
	}

	repositories {
		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}